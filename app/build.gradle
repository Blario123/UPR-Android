apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties importStrings = new Properties()
importStrings.load(new FileInputStream(file(
        "../upr/src/com/dabomstew/pkrandom/newgui/Bundle.properties")))
def trimPrefix = ~/^gui_/
def trimSuffix = ~/_?text$/

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "ly.mens.rndpkmn"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        importStrings.forEach { rawKey, value ->
            def key = (rawKey.toLowerCase().replaceAll("\\.", "_") - trimPrefix) - trimSuffix
            resValue "string", key, "\"$value\""
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main.java.srcDirs = ['src/main/kotlin']
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation project(':upr')
}
repositories {
    mavenCentral()
}
